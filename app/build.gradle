buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        //noinspection GradleDynamicVersion
        classpath 'me.tatarka:gradle-retrolambda:3.7.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    maven {
        name 'glide-snapshot'
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26

        renderscriptTargetApi 25
        vectorDrawables.useSupportLibrary = true

        applicationId "code.name.monkey.retromusic"
        versionCode 72
        versionName '6.0.200'

        multiDexEnabled true

        buildConfigField("String", "LASTFM_API_KEY", "\"${getProperty(getProperties('../public.properties'), 'LASTFM_API_KEY')}\"")
        buildConfigField("String", "GOOGLE_PLAY_LICENSE_KEY", "\"${getProperty(getProperties('../public.properties'), 'GOOGLE_PLAY_LICENSE_KEY')}\"")

    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'

            ext.enableCrashlytics = false // Disable fabric build ID generation for debug builds
        }
    }
    flavorDimensions "default"
    productFlavors {
        normal {
            versionCode defaultConfig.versionCode + 4000
            versionName defaultConfig.versionName + " BETA 5"
            dimension "default"
        }
        kitkat {
            minSdkVersion 19
            targetSdkVersion 19
            versionCode defaultConfig.versionCode + 2000
            versionName defaultConfig.versionName + " Beta 1"
            dimension "default"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '26.1.0'
                }
            }
        }
    }
}

@Nullable
def getProperties(String fileName) {
    final def Properties properties = new Properties()
    def file = file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

def getProperty(@Nullable Properties properties, String name) {
    return properties.getProperty(name) ?: "$name missing";
}

ext {
    supportLibVersion = "26.1.0"
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:multidex:1.0.2'
    compile "com.android.support:support-compat:$supportLibVersion"
    compile "com.android.support:support-v13:$supportLibVersion"
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:gridlayout-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:palette-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:support-annotations:$supportLibVersion" 
    compile "com.android.support:preference-v7:$supportLibVersion"
    compile "com.android.support:preference-v14:$supportLibVersion"
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.2'
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    compile 'com.afollestad:material-cab:0.1.12'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.5.0'
    compile 'com.github.kabouzeid:app-theme-helper:1.3.7'
    compile 'com.afollestad.material-dialogs:core:0.9.4.5@aar'
    compile 'com.afollestad.material-dialogs:commons:0.9.4.5@aar'
    compile 'com.github.kabouzeid:RecyclerView-FastScroll:1.0.16-kmod'
    compile 'com.github.kabouzeid:AndroidSlidingUpPanel:3.3.0-kmod3'
    compile 'me.zhanghai.android.materialprogressbar:library:1.4.1'
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.2'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.github.stfalcon:multiimageview:0.1'
    compile 'com.github.h6ah4i:android-advancedrecyclerview:7020c4c400'
    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    compile 'uk.co.chrisjenx:calligraphy:2.3.0'
    compile "com.andkulikov:transitionseverywhere:1.7.6"
}